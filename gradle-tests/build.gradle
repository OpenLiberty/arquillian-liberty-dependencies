buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath group: 'io.openliberty.tools', name: 'liberty-gradle-plugin', version: '3.9.1'
        classpath "io.spring.gradle:dependency-management-plugin:1.0.4.RELEASE"
    }
}

subprojects {
    apply plugin: 'war'

    apply plugin: 'java'
    apply plugin: 'liberty'

    apply plugin: "io.spring.dependency-management"

    buildscript {
        repositories {
            mavenLocal()
            mavenCentral()
        }
        dependencies {
            classpath group: 'io.openliberty.tools', name: 'liberty-gradle-plugin', version: '3.9.1'
            classpath "io.spring.gradle:dependency-management-plugin:1.0.4.RELEASE"
        }
    }

    sourceSets {
        main {
            java {
                srcDir "${rootProject.projectDir}/src/main/java/"
            }
            resources {
                srcDir "${rootProject.projectDir}/src/main/resources/"
            }
        }
        test {
            java {
                srcDir "${rootProject.projectDir}/src/test/java"
            }
        }
    }

    sourceCompatibility = 11
    targetCompatibility = 11
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    repositories {
        mavenLocal()
        mavenCentral()
    }
        
    liberty {
        server {
            serverXmlFile = file("${rootProject.projectDir}/src/test/resources/server.xml")
            name = 'ArquillianManagedServer'
         }
    }

    dependencyManagement {
        imports {
            mavenBom "org.jboss.arquillian:arquillian-bom:1.9.1.Final"
            mavenBom "org.jboss.arquillian:arquillian-build:1.9.1.Final"
            mavenBom "org.jboss.arquillian.jakarta:arquillian-jakarta-bom:10.0.0.Final"
        }
    }

    dependencies {
        implementation ('jakarta.enterprise:jakarta.enterprise.cdi-api:3.0.0')
       
        testImplementation group: "io.openliberty.arquillian", name: "arquillian-liberty-managed-jakarta-junit", version: dependencyBundleVersion
        testImplementation group: "junit", name: "junit", version: "4.13.2"
        testImplementation ('org.jboss.shrinkwrap:shrinkwrap-api')
        testImplementation files("${System.properties['java.home']}/../lib/tools.jar")
    }

    tasks.withType(Test) {
        group 'Verification'
        testLogging.showStandardStreams = true
        reports.html.destination = file("$buildDir/reports/")
        reports.junitXml.destination = file("$buildDir/test-results/")
        include '**/*'
        dependsOn 'deploy', 'testClasses', 'configureArquillian'
    }
}
